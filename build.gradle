plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'net.java.dev.jna', name: 'jna-platform', version: 'latest.release'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of 11
    }
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from project.components.java
            pom {
                name = 'log-files'
                description = 'Java library to get a cross-platform log file for desktop application'
                url = 'https://github.com/cmosher01/log-files'
                inceptionYear = '2019'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'cmosher01'
                        name = 'Christopher Alan Mosher'
                        email = 'cmosher01@gmail.com'
                        url = 'https://mosher.mine.nu'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/cmosher01/log-files.git'
                    developerConnection = 'scm:git:ssh://github.com/cmosher01/log-files.git'
                    url = 'http://github.com/cmosher01/log-files'
                }
            }
        }
    }
}
tasks.assemble.dependsOn tasks.generatePomFileForMavenPublication

signing {
    sign publishing.publications.maven
}
tasks.assemble.dependsOn tasks.signMavenPublication

publishing {
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

group = 'nu.mine.mosher.io'

version = '1.0.1'
